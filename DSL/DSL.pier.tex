% -*- mode: latex; -*-
\ifx\wholebook\relax\else

	% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../_support/latex/common.tex}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../_support/latex/common.tex}
%	\usepackage{a4wide}
% --------------------------------------------    \graphicspath
	\begin{document}
\fi
\sloppy

\chapter{Crafting a little Embedded DSL}
In this chapter we will develop a simple domain specific language (DSL) for rolling dice. Players of games such as Dungeon and Dragons are familiar with the DSL we will implement. An example of such DSL is \ct{2 D20 + 1 D6} which means that we should roll two times a 20-faces dice and one time a 6 faces dice. This chapter will show how we can (1) simply reuse traditional operator such as \ct{+}, (2) develop an embedded DSL and (3) show a nice usage of class extensions.
\section{ Getting started}
Using the code browser, define a package named \ct{Dice} or any name your like.
\subsection{ Defining the class Dice}

\begin{code}{}
Object subclass: #Dice
	instanceVariableNames: 'faces'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Dice'
\end{code}


In the \ct{initialize} category, define the method \ct{initialize} as follows. It simply set the default number of faces to 6. 


\begin{code}{}
Dice>>initialize 
	super initialize.
	faces := 6
\end{code}

\subsection{ Creating a test }
It is always empowering to verify that the code we write is always working as we defining it. For 
this purpose we will create a unit test. Remember unit testing was promoted by K. Beck first in Smalltalk. Nowadays this is a common practice but this is always useful to remember our roots!

So we define the class \ct{DiceTest} as a subclass of \ct{TestCase}.


\begin{code}{}
TestCase subclass: #DiceTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Dice'
\end{code}



\begin{code}{}
DiceTest>>testInitializeIsOk

		self shouldnt: [ Dice new ] raise: Error
\end{code}

\section{ Rolling a dice}
To roll a dice we will use the method from Number \ct{atRandom} which draws randomly a 
number between one and the receiver. For example \ct{10 at random} draws number between 1 to 10. 
Therefore we define the method \ct{roll} as follows.


\begin{code}{}
Dice>>roll

	^ faces atRandom
\end{code}


Now we can create an instance \ct{Dice new} and send it the message  \ct{roll} and get a result.
Do \ct{Dice new inspect} and then type in the bottom pane \ct{self roll}.
You should get an inspector like the one shown in Figure \ref{figDiceNoDetail}. With it you can interact with the dice by writing expression in the bottom pane.


\begin{figure}

\begin{center}
\includegraphics[width=0.5\textwidth]{figures/DiceNoDetail.pdf}\caption{Inspecting and interacting with a Dice.\label{figDiceNoDetail}}\end{center}
\end{figure}


 
\subsection{ Creating another test}
We will define a test that verifies that rolling a new created dice with a default 6 faces only returns 
value comprised between 1 and 6. This is what the following test method is actually specifying.
 

\begin{code}{}
DiceTest>>testRolling
	
	| d |
	d := Dice new. 
	10 timesRepeat: [ self assert: (d roll between: 1 and: 6) ]
\end{code}


Note that often it is better to define the test even before the code it tests. Why? Because you can think about the API of your objects and a scenario that illustrate their correct behavior. It helps you to program your solution.
\section{ Instance creation interface}
We would like to get a simpler way to create Dice. For example we want to create a 20-faces dice as follows: \ct{Dice faces: 20}. Let us define a test for it.


\begin{code}{}
DiceTest>>testCreationIsOk
	
		self shouldnt: [ Dice faces: 20 ] raise: Error
\end{code}


We define the class method \ct{faces:} as follows. It creates an instance then send the message \ct{faces:} to it and returns the instance.


\begin{code}{}
Dice class>>faces: aNumber

	| instance |
	instance := self new.
	instance faces: aNumber.
	^ instance
\end{code}


This method is strictly equivalent to the one below. The trick is that \ct{yourself} is a simple method defined on \ct{Object} class. \ct{yourself} returns the receiver of a message and the use of \ct{;} sends the message to the receiver of the previous message (here \ct{faces:}), therefore \ct{yourself} is sent to the object resulting from the execution of the expression \ct{self new} (which returns a new instance of the class \ct{Dice}). 


\begin{code}{}
Dice class>>faces: aNumber
	
	^ self new faces: aNumber; yourself
\end{code}


If you execute it will not work since we did not create yet the method \ct{faces:} this is now the time to define it.

\begin{code}{}
Dice>>faces: aNumber

	faces := aNumber
\end{code}


Now your tests should run. 

So even if the class \ct{Dice} could implement more behavior, we are ready to implement a dice handle. 
\section{ First specification of a dice handle}
Let us define a new class \ct{DiceHandle} that represents a dice handle. 
Here is the API that we would like to offer for now. We create a new instance of the handle then add some dice to it.


\begin{code}{}
DiceHandle new 
	addDice: (Dice faces: 6);
	addDice: (Dice faces: 10);
	yourself
\end{code}


Of course we will define a test for this new class. We define the class \ct{DiceHandleTest} as follow. 
\subsection{ Testing Handle Dice}

\begin{code}{}
TestCase subclass: #DiceHandleTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Dice'
\end{code}


We define a new test method as follows. 


\begin{code}{}
DiceHandleTest>>testCreationAdding

	| handle |
	handle := DiceHandle new 
			addDice: (Dice faces: 6);
			addDice: (Dice faces: 10);
			yourself.
	self assert: handle diceNumber = 2.
\end{code}


In fact we can do it better and add a new test method that verifies that we can even add
two dices having the same number of faces. 


\begin{code}{}
DiceHandleTest>>testAddingTwiceTheSameDice

	| handle |
	handle := DiceHandle new 
			addDice: (Dice faces: 6);
			yourself.
	self assert: handle diceNumber = 1.
	handle addDice: (Dice faces: 6).
	self assert: handle diceNumber = 2.
\end{code}

\subsection{ Defining the DiceHandle class}
This class defines one instance variable to hold dices it contains. 


\begin{code}{}
Object subclass: #DiceHandle
	instanceVariableNames: 'dice'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Dice'
\end{code}


We simply initialize it so that its instance variable \ct{dice} contains an \ct{OrderedCollection}.


\begin{code}{}
DiceHandle>>initialize
	super initialize.
	dice := OrderedCollection new.
\end{code}


Then we define a simple method to add a dice to the list of dices of the handle.


\begin{code}{}
DiceHandle>>addDice: aDice 
	dice add: aDice
\end{code}


Now you can execute the code snippet and inspect it. You should get an inspector as shown in Figure \ref{diceHandleNoDetail} 

\begin{code}{}
DiceHandle new 
	addDice: (Dice faces: 6);
	addDice: (Dice faces: 10);
	yourself
\end{code}



\begin{figure}

\begin{center}
\includegraphics[width=0.5\textwidth]{figures/DiceHandleNoDetail.pdf}\caption{Inspecting a Dice.\label{diceHandleNoDetail}}\end{center}
\end{figure}


Finally we should add the method \ct{diceNumber} to the \ct{DiceHandle} class to be able to get the number of dice of the handle. We just returns the size of the dices collection.


\begin{code}{}
DiceHandle>>diceNumber  

	^ dice size
\end{code}


Now you tests should run and this is good moment to save and publish your code. 
\section{ Improving programmer experience}
Now when you open an inspector you cannot see well the dices that compose the dice handle. Click on the dice instance variable and you will only get a list of \ct{a Dice} without further information. 


\begin{code}{}
DiceHandle new 
	addDice: (Dice faces: 6);
	addDice: (Dice faces: 10);
	yourself
\end{code}


So we will enhance the \ct{printOn:} method of the Dice class to provide more information. Here we simply add the number of faces surrounded by parenthesis.


\begin{code}{}
Dice>>printOn: aStream

	super printOn: aStream.
	aStream nextPutAll: ' (', faces printString, ')'
\end{code}


Now in your inspector you can see effectively the number of faces a dice handle has as shown by Figure \ref{diceDetail} and it is now easier to check the dice contained inside a handle (See Figure \ref{diceHandleDetail}).


\begin{figure}

\begin{center}
\includegraphics[width=0.5\textwidth]{figures/DiceDetail.png}\caption{Dice details.\label{diceDetail}}\end{center}
\end{figure}
 


\begin{figure}

\begin{center}
\includegraphics[width=0.5\textwidth]{figures/DiceHandleDetail.png}\caption{Dice Handle with more information.\label{diceHandleDetail}}\end{center}
\end{figure}
 
\section{ Rolling a dice handle}
Now we can define the rolling of a handle of dice by simply summing the dice rolls.

\begin{code}{}
DiceHandle>>roll
	
	| res |
	res := 0.
	dice do: [ :each | res := res + each roll ].
	^ res
\end{code}


Now we can send the message \ct{roll} to a dice handle.

\begin{code}{}
handle := DiceHandle new 
		addDice: (Dice faces: 6);
		addDice: (Dice faces: 10);
		yourself.
handle roll
\end{code}

\section{ Role playing syntax}
Now we are ready to offer a syntax following practice of role playing game, i.e., using \ct{2 D20} to create a handle of two 20 faces dice.  For this purpose we will define class extensions: we will define methods in the class \ct{Integer} but these methods will be only available when the package Dice will be loaded. 

But first let us specify  what we would like to obtain by writing a new  test in the class \ct{DiceHandleTest}. Remember
to always take  any opportunity to write tests.  When we execute \ct{2 D20} we  should get a new handle  composed of two
dice and can verify that. This is what the method \ct{testSimpleHandle} is doing.


\begin{code}{}
DiceHandleTest>>testSimpleHandle

	self assert: 2 D20 diceNumber = 2.
\end{code}


Verify that the test is not working! It is much more satisfactory to get a test running when it was not working before. Now define the method \ct{D20} with a category name that is \ct{*Dice} (if you named your package Dice). This method simply creates a new dice handle, add the correct number of dice to this handle and return it.


\begin{code}{}
Integer>>D20
	
	| handle |
	handle := DiceHandle new.
	self timesRepeat: [ handle addDice:  (Dice faces: 20)].
	^ handle
\end{code}


Now you test should pass and this is probably a good moment to save your work either by publishing your package to SmalltalkHub and to save your image. 

Now we could do the same for the default dice with different faces number: 4, 6, 10, and 20.
But we should avoid duplicating logic and code. So first we will introduce a new method \ct{D:} and based on it we will define all the others


\begin{code}{}
Integer>>D: anInteger
	
	| handle |
	handle := DiceHandle new.
	self timesRepeat: [ handle addDice:  (Dice faces: anInteger)].
	^ handle
\end{code}



\begin{code}{}
Integer>>D4
	
	^ self D: 4
\end{code}



\begin{code}{}
Integer>>D6
	
	^ self D: 6
\end{code}



\begin{code}{}
Integer>>D10
	
	^ self D: 10
\end{code}



\begin{code}{}
Integer>>D20
	
	^ self D: 20
\end{code}


We have now a compact form to create dice and we are ready for the last part: the addition of handles. 
\subsection{ Handle's Addition}
Now we can simply support the addition of handles. But of course let's write a test first.


\begin{code}{}
DiceHandleTest>>testSumming

	| handle |
	handle := 2 D20 + 3 D10.
	self assert: handle diceNumber = 5.
\end{code}


We will define a method \ct{+} on the HandleDice class. In other languages this is often not possible or is based on operator overloading. In Pharo \ct{+} is just a message as any other, therefore we can define it on the classes we want.

Now we should ask ourself what is the semantics of adding two handles. Should we modify the receiver of the expression or create a new one. We preferred a more functional style and choose the create a third one. 

The method \ct{+} creates a new handle then add to it the dice of the receiver and the one of the handle passed as argument to the message. Finally we return it. 


\begin{code}{}
DiceHandle>>+ aDiceHandle

	| handle |
	handle := self class new.
	self dice do: [ :each | handle addDice: each ].
	aDiceHandle dice do: [ :each | handle addDice: each ].
	^ handle
\end{code}


Now we can execute the method \ct{(2 D20 + 1 D6) roll} nicely and start playing role playing games, of course.
\section{ Conclusion}
This chapter illustrates how to create a small DSL based on the definition of some domain classes (here \ct{Dice} and  
 \ct{DiceHandle}) and the extension of core class such \ct{Integer}.
It shows that in Pharo we can use usual operators to express natural models.



\ifx\wholebook\relax\else
   \bibliographystyle{jurabib}
   \nobibliography{scg}\end{document}
\fi
